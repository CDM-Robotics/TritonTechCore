plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2025.3.2"
    id 'maven-publish'
}

group = 'org.tritontech'
version = '1.0.16'
sourceCompatibility = '17'
targetCompatibility = '17'

def versionFile = file("$buildDir/resources/main/org/tritontech/core/version.properties")

task generateVersionProperties {
    description 'Generates version.properties with the project version'
    group 'Build'
    outputs.file versionFile
    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.text = "version=${project.version}"
    }
}

processResources.dependsOn generateVersionProperties

repositories {
  mavenCentral()
  maven { url 'https://frcmaven.wpi.edu/artifactory/release' }
  maven { url 'https://frcmaven.wpi.edu/artifactory/development' }
  maven { url 'https://maven.revrobotics.com/' }  // REV Robotics Maven repo
  maven { url 'https://dev.studica.com/maven/release/2025/' }  // Studica Maven repo
  maven { url 'https://3015rangerrobotics.github.io/pathplannerlib/repo' }  // PathplannerLib Maven repo
//  maven { url 'https://lib.choreo.autos/dep', 'https://repo1.maven.org/maven2'}
}

dependencies {
  annotationProcessor wpi.java.deps.wpilibAnnotations()
  implementation wpi.java.deps.wpilib()
  implementation wpi.java.vendor.java()

  // WPILib Java dependencies
  implementation 'edu.wpi.first.wpilibj:wpilibj-java:2025.3.2'  // Pinned to 2025.3.2
  implementation 'edu.wpi.first.wpiutil:wpiutil-java:2025.3.2'
  implementation 'edu.wpi.first.wpimath:wpimath-java:2025.3.2'
  implementation 'edu.wpi.first.wpiunits:wpiunits-java:2025.3.2'
  implementation 'edu.wpi.first.ntcore:ntcore-java:2025.3.2'
  implementation 'edu.wpi.first.cscore:cscore-java:2025.3.2'
  implementation 'edu.wpi.first.cameraserver:cameraserver-java:2025.3.2'
  implementation 'edu.wpi.first.hal:hal-java:2025.3.2'
  implementation 'edu.wpi.first.thirdparty.frc2025.opencv:opencv-java:4.10.0-2'

  // REV Robotics dependency
  implementation 'com.revrobotics.frc:REVLib-java:2025.0.3'

  // Studica dependency
  implementation 'com.studica.frc:Studica-java:2025.0.1'

  // PathplannerLib dependency
  implementation 'com.pathplanner.lib:PathplannerLib-java:2025.2.7'

  // ChoreoLib dependency
  //implementation 'org.choreo:choreo-lib:2025.0.3'
  //implementation 'choreo:choreo-lib:2025.0.3'

  // Optional dependencies
  implementation 'org.ejml:ejml-simple:0.43.1'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'

  // Test dependencies
  testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

import groovy.json.JsonSlurper

def vendordepsDir = file('vendordeps')
def vendorJsons = vendordepsDir.listFiles()?.findAll { it.name.endsWith('.json') } ?: []

vendorJsons.each { jsonFile ->
    def json = new JsonSlurper().parse(jsonFile)

    // ✅ Add each Maven URL separately
    json.mavenUrls?.each { urlString ->
        repositories {
            maven { url urlString }
        }
    }

    // ✅ Add each Java dependency
    json.javaDependencies?.each { dep ->
        dependencies {
            implementation "${dep.groupId}:${dep.artifactId}:${dep.version}"
        }
    }
}

test {
  useJUnitPlatform()
  testLogging {
    events 'failed'
    exceptionFormat 'full'
  }
}

javadoc {
  options {
    links 'https://docs.oracle.com/en/java/javase/17/docs/api/',
          'https://github.javadoc.io/allwpilib/docs/release/java/', 
          'https://codedocs.revrobotics.com/'
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'org.tritontech'
      artifactId = 'TritonTechCore'
      version = '1.0.16'
      from components.java
    }
  }
  repositories {
    mavenLocal()  // Local publishing for testing
  }
}

// Task to clear build cache
task clearBuildCache(type: Delete) {
    description 'Clears the build cache to force dependency re-resolution'
    group 'Build'
    delete fileTree(dir: 'build/tmp/.cache')
}

// Task to generate TritonTechCore.json with dynamic user name and version
import groovy.json.JsonOutput
task generateVendorJson {
    description 'Generates TritonTechCore.json with dynamic user name and project version'
    group 'WPILib'

    doLast {
        def userName = System.getProperty('user.name')
        def json = [
            fileName: 'TritonTechCore.json',
            name: 'TritonTechCore',
            version: project.version,
            uuid: 'a121b32c-1adf-484c-bb57-0d959dbbe62f',
            frcYear: '2025',
            mavenUrls: ["file:///C:/Users/${userName}/.m2/repository/"],
            jsonUrl: "file:///C:/Users/${userName}/TritonTechCore/TritonTechCore.json",
            javaDependencies: [
                [
                    groupId: 'org.tritontech',
                    artifactId: 'TritonTechCore',
                    version: project.version
                ]
            ],
            jniDependencies: [],
            cppDependencies: []
        ]
        def jsonString = JsonOutput.prettyPrint(JsonOutput.toJson(json))
        file('TritonTechCore.json').text = jsonString
    }
}

// REMEMBER THIS COMMAND, it cleans up the vccode cache and project files!!!
// ./gradlew cleanEclipse eclipse

// Make publishToMavenLocal depend on generateVendorJson
publishToMavenLocal.dependsOn generateVendorJson
build.dependsOn generateVendorJson